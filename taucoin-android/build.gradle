apply plugin: 'com.android.application'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "io.taucoin.torrent.publishing"
        minSdkVersion 22
        targetSdkVersion 28
        versionCode 12
        versionName "1.6.5"
        flavorDimensions("default")

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        resConfigs "en-rUS"
        renderscriptTargetApi 22
        renderscriptSupportModeEnabled true

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64'
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    sourceSets {
        release {
            assets.srcDirs = ['assets', 'src/main/assets/release']
        }
        debug {
            assets.srcDirs = ['assets', 'src/main/assets/debug']
        }
    }

    signingConfigs {
        myConfig {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'AndroidDebugKey'
            keyPassword 'android'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled true
            signingConfig signingConfigs.myConfig
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled true
            signingConfig signingConfigs.myConfig
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/proguard/androidx-annotations.pro'
        exclude 'META-INF/proguard/coroutines.pro'
        exclude 'META-INF/androidx.preference_preference.version'
        exclude 'kotlin/collections/MapWithDefault.kotlin_metadata'
    }

    configurations.all {
        resolutionStrategy {
            force 'com.google.code.findbugs:jsr305:2.0.1'
            force "androidx.camera:camera-camera2:1.0.2"
            force 'androidx.camera:camera-core:1.0.2'
        }
    }

    productFlavors {
        develop {
            buildConfigField "String", "SPLASH_APP_NAME", "\"TAU App Develop\""     // 启动页文案
            buildConfigField "Integer", "UI_RELEASE_LOG_LEVEL", "10000"             // Level.DEBUG
            buildConfigField "Integer", "TAU_RELEASE_LOG_LEVEL", "7"                // TauLogLevel.DEBUG
            buildConfigField "String", "DHT_BOOTSTRAP_NODES", "\"tau://6e6170b970bae896803ecb1051653" +
                    "7aba6ab974ac6aad7b898f609117fbafe2b@13.229.53.249:7882\""
        }

        beta {
            buildConfigField "String", "SPLASH_APP_NAME", "\"TAU App Beta\""        // 启动页文案
            buildConfigField "Integer", "UI_RELEASE_LOG_LEVEL", "20000"             // Level.INFO
            buildConfigField "Integer", "TAU_RELEASE_LOG_LEVEL", "6"                // TauLogLevel.INFO
            buildConfigField "String", "DHT_BOOTSTRAP_NODES", "\"tau://83024767468B8BF8DB868F336596C" +
                    "63561265D553833E5C0BF3E4767659B826B@13.229.53.249:6882\""
        }

        official {
            buildConfigField "String", "SPLASH_APP_NAME", "\"TAU App\""             // 启动页文案
            buildConfigField "Integer", "UI_RELEASE_LOG_LEVEL", "30000"             // Level.WARN
            buildConfigField "Integer", "TAU_RELEASE_LOG_LEVEL", "4"                // TauLogLevel.WARN
            buildConfigField "String", "DHT_BOOTSTRAP_NODES", "\"tau://83024767468B8BF8DB868F336596C" +
                    "63561265D553833E5C0BF3E4767659B826B@13.229.53.249:6882\""
        }
    }
}

configurations {
    // This package is excluded here. This package will be connected with com.google.guava
    // Conflicts, resulting in compilation failure
    all*.exclude group: 'com.google.guava', module: 'listenablefuture'
}

def ROOM_VERSION = "2.2.6"
def DAGGER_VERSION = "2.27"
def CASTLE_VERSION = '1.51.0.0'

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    /* Testing */
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.robolectric:robolectric:4.3.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation "androidx.room:room-testing:$ROOM_VERSION"
    androidTestImplementation "com.google.dagger:dagger:$DAGGER_VERSION"
    testAnnotationProcessor "com.google.auto.service:auto-service:1.0-rc6"
    /* AndroidX libraries */
    // pictureselector 中含有高版本appcompat
//    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0-beta02'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.recyclerview:recyclerview-selection:1.2.0-alpha01'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.room:room-runtime:$ROOM_VERSION"
    implementation "androidx.room:room-rxjava2:$ROOM_VERSION"
    annotationProcessor "androidx.room:room-compiler:$ROOM_VERSION"
    /* For schemas import */
    implementation 'androidx.work:work-runtime:2.5.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.paging:paging-runtime:2.1.2'
    /* ReactiveX */
    implementation 'io.reactivex.rxjava2:rxjava:2.2.9'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    /* Logger */
    implementation 'com.github.tony19:logback-android-core:1.1.1-6'
    implementation('com.github.tony19:logback-android-classic:1.1.1-6') {
        exclude group: 'com.google.android'
    }
    implementation 'org.slf4j:slf4j-api:1.7.25'
    /* Other */
    implementation 'net.cachapa.expandablelayout:expandablelayout:2.9.2'
    implementation 'com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:1.0.0'
    implementation 'com.github.anthonynsimon:jurl:v0.4.2'
    implementation 'com.jaredrummler:colorpicker:1.1.0'
    /* Don't use Commons IO >= 2.6, because they uses Java NIO, supported only from API 26 */
    implementation 'commons-io:commons-io:2.5'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.takisoft.preferencex:preferencex:1.1.0'
    implementation 'com.leinardi.android:speed-dial:3.1.1'
    implementation 'net.java.dev.jna:jna:5.5.0@aar'
    // 此库只支持API >= 21
    //    implementation 'com.github.minminaya:LGMRoundButton:v1.2'
    implementation 'com.github.czy1121:roundbutton:1.1.0'
    implementation 'com.yanzhenjie.recyclerview:x:1.3.2'
    //    implementation 'com.facebook.stetho:stetho:1.5.0'
//    implementation 'com.github.hf:leveldb:1.19.0@aar'
    // 数据库调试
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'
    // 内存检测工具，2.X无需在Application中添加代码
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'
    // 图表
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    // picture selector
    implementation 'io.github.lucksiege:pictureselector:v2.7.3-rc09'
    implementation 'com.github.bumptech.glide:glide:4.12.0'
//    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'
    // eventbus
    implementation 'org.greenrobot:eventbus:3.1.1'

    // 加密
    implementation "com.madgag.spongycastle:core:${CASTLE_VERSION}"
    implementation "com.madgag.spongycastle:prov:${CASTLE_VERSION}"
    implementation 'com.google.guava:guava:24.1-jre'

    // float menu
    implementation 'com.noober.floatmenu:common:1.0.4'

    // file compress
    implementation 'org.apache.commons:commons-compress:1.18'
}
